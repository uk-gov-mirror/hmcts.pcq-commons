
plugins {
    id 'java'
    id 'checkstyle'
    id 'uk.gov.hmcts.java' version '0.12.63'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'org.springframework.boot' version '3.3.0'
    id "io.freefair.lombok" version "8.6"
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}


def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: getCurrentGitBranch()

group 'uk.gov.hmcts.reform.pcq'
// Do not change this version number
version buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://www.opensource.org/licenses/MIT"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/pcq-commons"
    }
}

task unit(type: Test, description: 'Runs the unit tests.', group: 'Verification') {
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
    useJUnitPlatform()
}


java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'pcq-commons'
            version project.version
        }
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.17.0'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDirectory = new File(rootDir, 'config/checkstyle')
}

def versions = [
        feignForm         : '3.8.0',
        jackson           : '2.17.1',
        jjwt              : '0.12.5',
        jupiter           : '5.10.2',
        lombok            : '1.18.32',
        mockito           : '5.12.0',
        springBoot        : '3.3.0'
]

dependencies {
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'io.github.openfeign.form', name: 'feign-form-spring', version: versions.feignForm
    implementation group: 'io.github.openfeign.form', name: 'feign-form', version: versions.feignForm
    implementation group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.14.0'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '4.0.5'
    implementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.2'
    implementation group: "io.jsonwebtoken", name: "jjwt-api", version: versions.jjwt

    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: versions.jjwt
    runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: versions.jjwt

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.12.0'
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.jupiter
    testImplementation group: 'org.mockito', name: 'mockito-core', version: versions.mockito

    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.jupiter
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.10.2'

}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
    }
    systemProperty "user.timezone", "UTC"
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

def getCurrentGitBranch() {
    def gitBranch = "DEV-SNAPSHOT"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}
